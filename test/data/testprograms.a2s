def title	"TestPrograms"
def version	"1.0"
def description	"Test programs for rendertest.c, wavestress.c etc"
def author	"David Olofson"
def copyright	"Copyright 2013-2014 David Olofson"
def license	"Public domain. Do what you like with it. NO WARRANTY!"
def a2sversion	"1.9"

// Play specified wave with simple envelope (for rendertest.c)
PlayTestWave(P V=1 W=sine)
{
	struct { wtosc; panmix }
	w W; p P
	a V; d 10
	end
.rel	a 0; d 500
	1() { force rel }
}

// Play specified wave with simple envelope (for waveupload.c)
PlayTestWave2(P V=1 W=sine)
{
	struct { wtosc; panmix }
	w W; p P
	a V; set a
	end
.rel	a 0; d 100
	1() { force rel }
}

// Play specified wave - no messages (for wavestress.c)
PlayTestWave3(P V=1 W=sine)
{
	struct { wtosc; panmix }
	w W; p P
	a V; set a
	d 100
	a 0; d 100
}

// Simple "instrument" for voicestress.c
PlayTestNote(P V=1)
{
	struct { wtosc; panmix }
	w triangle; p (P + 1)
	a V; d 1
	p P
	while a > 0.01 {
		*a .97; d 1
	}
	end
.rel	a 0; d 10
	1() { force rel }
}

// Short "blip" for timingtest.c
PlayBlip(P V=1)
{
	struct { wtosc; panmix }
	w sine
	p P
	a V; set a
	a 0; d 1
}

// Streaming voice program for streamstress.c
StreamStressVoice(V=1 P D)
{
	struct { xsource; panmix }
	pan P; set pan
	vol 0; set vol
	vol V; d (D / 2)
	vol 0; d (D / 2)
}

// Streaming voice program for streamtest.c
StreamVoice()
{
	struct { xsource; panmix }
}

// Capture voice program for streamtest.c
CaptureVoice()
{
	struct { inline 0 1; xsink 1 0 }
}
