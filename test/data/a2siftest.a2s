def title	"A2SIfTest"
def version	"1.0"
def description	"Audiality 2 Script 'if' statement test"
def author	"David Olofson"
def copyright	"Copyright 2016 David Olofson"
def license	"Public domain. Do what you like with it. NO WARRANTY!"
def a2sversion	"1.9"

// If the test works correctly, it should compile - of course - and generate
// debug output with a contiguous range of values from 1 through 10.

export Song(P V=1 L=0)
{
	!x 0
	while x <= 10 {
		// Parentheses around expressions are optional, but only with a
		// braced block body.
		if x == 1 { debug 1 }

		// Single statement allowed, but only with simple expression.
		if (x == 2) debug 2

		// Opening brace of body can be placed on the same line...
		if x == 3 {
			debug 3
		}

		// ...or on the next line.
		if x == 4
		{
			debug 4
		}

		// Same as the above, for 'else'.
		// NOTE: We do not allow 'else' after non-braced statements!
		if x != 5 {} else { debug 5 }
		if x != 6 {} else debug 6
		if x != 7 {} else {
			debug 7
		}
		if x != 8 {} else
		{
			debug 8
		}

		// We also allow 'else' on a new line, provided the "true"
		// statement is a braced body.
		if x != 9
		{
		}
		else
		{
			debug 9
		}

		// All done!
		if (x == 10) debug 10

		+x 1
	}
}
